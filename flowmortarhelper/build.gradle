buildscript {
    repositories {
        mavenCentral()

    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}
plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.dcendents.android-maven'

Map<String, String> versions = [
        support: '23.0.1',
        mortar       : '0.17',
        butterknife  : '7.0.1',
        dagger       : '2.0.1',
        flow         : '0.10',
        gson         : '2.3',
        timber       : '2.2.2',
        realm        : '0.81.1',
        leakcanary   : '1.3.1',
        javax_annotation: '10.0-b28',
        guava        : '18.0'
]


def gitCount = 'git rev-list master --first-parent --count'.execute([], project.rootDir).text.trim()

//Version information
def versionMajor = 0
def versionMinor = 1
def versionBuild = (gitCount?.trim()) ? gitCount.toInteger() : 0

project.ext.versionCode = versionMajor * 1000 + versionMinor * 100 + versionBuild
project.ext.versionName = "${versionMajor}.${versionMinor}"

version = project.versionName

def siteUrl = 'https://github.com/iridiobis/FlowMortarHelper'      // Homepage URL of the library
def gitUrl = 'https://github.com/iridiobis/FlowMortarHelper.git'   // Git repository URL
group = "com.github.iridiobis.flowmortarhelper"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 16
        versionCode project.versionCode
        versionName project.versionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = 'flow-mortar-helper'
        userOrg = bintrayUser
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/iridiobis/FlowMortarHelper'
        version {
            name = project.versionName
            vcsTag = project.versionName
        }
    }

}

dependencies {
    compile "com.android.support:appcompat-v7:$versions.support"
    compile "com.android.support:design:$versions.support"
    compile "com.android.support:support-annotations:$versions.support"
    compile "com.google.guava:guava:$versions.guava"
    compile "com.google.dagger:dagger:$versions.dagger"
    compile "com.squareup.mortar:mortar:$versions.mortar"
    compile "com.squareup.flow:flow:$versions.flow"
    compile "com.squareup.flow:flow-path:$versions.flow"
    compile "com.google.code.gson:gson:$versions.gson"
    compile "com.jakewharton.timber:timber:$versions.timber"
    apt "com.google.dagger:dagger-compiler:$versions.dagger"
    provided "org.glassfish:javax.annotation:$versions.javax_annotation"
}

android.libraryVariants.all { variant ->
//
//    def name = variant.name.capitalize()
//    task "javadoc$name"(type: Javadoc, dependsOn: "assemble$name") {
//        description = "Generates javadoc for build $name"
//        destinationDir = new File(destinationDir, variant.baseName)
//        source = files(variant.javaCompile.source)
//        classpath = files(android.getBootClasspath(), variant.javaCompile.classpath)
//        exclude '**/R.html', '**/R.*.html', '**/index.html'
//        failOnError false
//    }
//
//    task "javadocJar$name"(type: Jar, dependsOn: "javadoc$name") {
//        classifier = 'javadoc'
//        from tasks."javadoc$name".destinationDir
//    }

    def baseName = "fmh"
    def builtName = variant.buildType.name

    if (builtName != 'release') {
        baseName += "-${builtName}"
    }

    baseName += "-v${project.versionName}.aar"

    variant.outputs.each { output ->
        def alignedOutputFile = output.outputFile

        // Customise APK filenames (to include build version)
        if (variant.buildType.zipAlignEnabled) {
            // normal APK
            output.outputFile = new File(alignedOutputFile.parent, baseName)
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'Flow Mortar Helper'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'iridiobis'
                        name 'Jose Luis Benito'
                        email 'iridiobis@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

